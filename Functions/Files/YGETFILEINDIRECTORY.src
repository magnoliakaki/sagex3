#############################################################
# GET_FILE_IN_DIRECTORY returns char 
# Returns the SYSTEME2 parameter (i.e. the cmd command) to find all files in a directory 
# @param    PATH (char) : relative or absolute path
# @param    PREFISSO (char) : files prefix
# @param    ESTENSIONE (char) : files extension 
# @param    FILE_ONLY (integer) :
#               0 = all files types
#               1 = only files that are not directories
#               2 = only directories 
# @param    SUBDIRECTORIES (integer) : 
#               0 = this directory
#               2* = this directory and all its subdirectories (* the command will return the file path, not the name+extension)
# @param    SORT (integer) : 
#               0 = no ordering
#               1 = ascending alphabetical ordering 
# You can find an example at the end of the file.

Funprog GET_FILE_IN_DIRECTORY(PATH, PREFISSO, ESTENSIONE, FILE_ONLY, SUBDIRECTORIES, SORT)
    Variable Char   PATH()
    Value Char      PREFISSO()
    Value Char      ESTENSIONE()
    Value Integer   FILE_ONLY
    Value Integer   SUBDIRECTORIES
    Value Integer   SORT
    Local Char      COMANDO(250)
    PATH = filpath(PATH,"","")
    COMANDO = "dir"
    If (PATH<> "")
        COMANDO -= PATH
    Endif
    COMANDO+="\"
    If (PREFISSO<>"")
        COMANDO +=PREFISSO
    Endif
    COMANDO+="*."
    If (ESTENSIONE<>"")
        COMANDO+=ESTENSIONE
    Else
        COMANDO+="*"
    Endif
    Case FILE_ONLY
        When 1 : COMANDO-= "/a-d"
        When 2 : COMANDO-= "/ad"
    Endcase
    If(SUBDIRECTORIES)
        COMANDO-="/s"
    Endif
    COMANDO-= "/b 2>nul"
    If(SORT)
        COMANDO -= "| sort"
    Endif
End COMANDO

# Example

$EXAMPLE 
    Local Char COM(250)
    Local Char IMPORT_DIRECTORY (250) : [L]IMPORT_DIRECTORY = filpath("IMPORT\Directory", "", "")
    Local Char IMPORT_PATH (250)
    Local Char FILESLIST ([V]GDIMFIC) (0..1000)
    Local Integer FC, FILESCOUNT, J

    # Find all files in IMPORT_DIRECTORY of extension csv 
    [L]COM = func GET_FILE_IN_DIRECTORY([L]IMPORT_DIRECTORY, "", "csv", 1, 0, 0)
    Call SYSTEME2(adxmac(-1), [L]COM, "", [L]FC, [L]FILESLIST) From ORDSYS
    [L]FILESCOUNT = min([L]FC, dim([L]FILESLIST))
    # FILESCOUNT is the number of found files
    # FILESLIST is the list of the files 

    For J = 0 To [L]FILESCOUNT - 1
        [L]FILNAM = left$([L]FILESLIST(J), instr(0, [L]FILESLIST(J), ".") -1)
        # This will give the path to a single file:
        [L]IMPORTPATH = filpath([L]IMPORT_DIRECTORY, [L]FILNAM, "csv")
    Next J
Return 
